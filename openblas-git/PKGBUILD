# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

pkgname=openblas
pkgver=3270.ecbeb802
pkgrel=1  
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD"
arch=('x86_64')
url="http://www.openblas.net/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran')
provides=('blas=3.8.0' 'cblas=3.8.0' 'lapack=3.8.0')
conflicts=('blas' 'cblas' 'lapack')
source=("$pkgname"::'git+https://github.com/xianyi/OpenBLAS.git#branch=develop')
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"

  make NO_STATIC=1 NO_LAPACKE=1 NO_AFFINITY=1 USE_OPENMP=1 CFLAGS="-march=native" \
       MAJOR_VERSION=3 libs netlib shared
}

package() {
  cd "$srcdir/$pkgname"

  make PREFIX="$pkgdir"/usr MAJOR_VERSION=3 install
  rm -f "$pkgdir"/usr/include/lapacke*
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$pkgdir"/usr/lib/
  # BLAS
  ln -sf libopenblas_*.so libblas.so
  ln -sf libopenblas_*.so libblas.so.3

  # CBLAS
  ln -sf libopenblas_*.so libcblas.so
  ln -sf libopenblas_*.so libcblas.so.3
  ln -sf libopenblas_*.so libcblas.so.3.8.0

  # LAPACK
  ln -sf libopenblas_*.so liblapack.so
  ln -sf libopenblas_*.so liblapack.so.3
  ln -sf libopenblas_*.so liblapack.so.3.8.0

  sed -i -e "s%$pkgdir%%" "$pkgdir"/usr/lib/cmake/openblas/OpenBLASConfig.cmake
  sed -i -e "s%$pkgdir%%" "$pkgdir"/usr/lib/pkgconfig/openblas.pc
  ln -s openblas.pc "$pkgdir"/usr/lib/pkgconfig/blas.pc

  rmdir "$pkgdir"/usr/bin
}

# vim:set ts=2 sw=2 et:
