# Maintainer: Reto Brunner <brunnre8@gmail.com>
pkgname=networkd-notify-git
pkgver=r28.c2f3e71
pkgrel=1
pkgdesc='Desktop notification integration for systemd-networkd'
arch=('any')
url='https://gitlab.com/wavexx/networkd-notify'
license=('GPL3')
depends=('systemd' 'python' 'python-gobject' 'python-dbus' 'iw')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
groups=('modified')
source=("$pkgname"::'git+https://gitlab.com/wavexx/networkd-notify.git'
        '001-use-iw-and-increase-notification-duration.patch')
sha256sums=('SKIP'
            'a75bc93bbfb69657aedb03a62b46c804c2de64f73a386ac39f2d3a68b5728ad4')

pkgver() {
    cd "$srcdir/${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"

}

prepare() {
    cd "$srcdir/${pkgname}"
    patch -p1 < ../'001-use-iw-and-increase-notification-duration.patch'
}

package() {
    cd "$srcdir/${pkgname}"
    mkdir -p "$pkgdir/usr/bin"
    install -m755 "networkd-notify" "$pkgdir/usr/bin/networkd-notify"
}

