# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Isenmann <daniel @archlinux.org>

pkgbase=dhcp
pkgname=dhclient

# separate patch levels with a period to maintain proper versioning.
pkgver=4.4.2
pkgrel=2
arch=('x86_64')
license=('custom:isc-dhcp')
url="https://www.isc.org/software/dhcp"
makedepends=('bash' 'iproute2' 'openldap')
groups=('modified')
source=("https://downloads.isc.org/isc/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.gz"
        'dhclient@.service::https://git.archlinux.org/svntogit/packages.git/plain/trunk/dhclient@.service?h=packages/dhcp'
        '0002-iproute2.patch::https://git.archlinux.org/svntogit/packages.git/plain/trunk/0002-iproute2.patch?h=packages/dhcp'
        '0003-disable-syslog-for-info.patch')
sha256sums=('1a7ccd64a16e5e68f7b5e0f527fd07240a2892ea53fe245620f4f5f607004521'
            '86cd0b1e0ea1d47ab096f6ee925eee60545116fb887a155761eda589b30e4f0e'
            '837a64189b949afae951655546967cc8f17f2f2cf370faabff00575364f0fcf7'
            '3e393076780fb14e258a72a478295bc46842f1476f830fffa4274a3ddf77fe05')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # no ifconfig, use ip from iproute2
  patch -Np1 -i ../0002-iproute2.patch

  patch -Np1 -i ../0003-disable-syslog-for-info.patch
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./configure \
      --prefix=/usr \
      --sbindir=/usr/bin \
      --sysconfdir=/etc \
      --enable-binary-leases \
      --enable-paranoia \
      --with-ldap \
      --with-ldapcrypto \
      --with-srv-lease-file=/var/lib/dhcp/dhcpd.leases \
      --with-srv6-lease-file=/var/lib/dhcp/dhcpd6.leases \
      --with-cli-lease-file=/var/lib/dhclient/dhclient.leases \
      --with-cli6-lease-file=/var/lib/dhclient/dhclient6.leases

  make -j1
}

package(){
  pkgdesc="A standalone DHCP client from the dhcp package"
  depends=('glibc' 'bash' 'iproute2' 'run-parts')
  provides=('dhcp-client')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make -C client DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/dhclient"
  mv "${pkgdir}/etc/dhclient.conf.example" "${pkgdir}/usr/share/dhclient/"

  install -d "${pkgdir}/var/lib/dhclient"

  # install dhclient linux script
  install -m755 client/scripts/linux "${pkgdir}/usr/bin/dhclient-script"

  # install license
  install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/dhclient/LICENSE"

  # install systemd service unit
  install -m644 -D "$srcdir/dhclient@.service" "${pkgdir}/usr/lib/systemd/system/dhclient@.service"
}
