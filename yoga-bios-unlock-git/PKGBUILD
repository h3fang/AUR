# Maintainer: h3fang <hf.enigma@gmail.com>
pkgname=yoga-bios-unlock-git
_pkgname=yoga-bios-unlock
pkgver=r37.81ce322
pkgrel=1
pkgdesc="unlock the advanced menu of Lenovo Yoga laptop BIOS"
arch=('x86_64')
url="https://github.com/esno/yoga-bios-unlock"
license=('MIT')
groups=('modified')
depends=('glibc')
source=(
    "git+https://github.com/esno/yoga-bios-unlock"
    "0001-yoga-14sARE-2020.patch"
)
b2sums=('SKIP'
        'fa4d68440179d28c5797b524c441ff4e725c05353faa1f4d2bebd2d6456c53c094caf8dc52c034b5e36941780a1783ed53d3e87d9e2566393f7412b1146f5d95')

pkgver() {
    cd "$_pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_pkgname"
    patch -Np1 < "$srcdir/0001-yoga-14sARE-2020.patch"
    echo "SDK0L77769 WIN" >> board-versions.txt
    make headers
}

build() {
    cd "$srcdir/$_pkgname"
    make
}

package() {
    cd "$srcdir/$_pkgname"
    install -Dm 0644 "$_pkgname" "$pkgdir/usr/bin/$_pkgname"
}

