# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ariel AxionL <i@axionl.me>
# Contributor: Luke Yue <lukedyue@gmail.com>

pkgname=clash-dev-git
pkgver=20210408.r588.baf03b8
pkgrel=1
pkgdesc="A rule based proxy in Go"
arch=('x86_64')
url="https://github.com/Dreamacro/clash"
license=('GPL3')
makedepends=('go' 'git')
depends=('glibc')
conflicts=('clash')
groups=('modified')
source=("clash@.service"
        "clash_user.service"
        "$pkgname::git+https://github.com/Dreamacro/clash.git#branch=dev")
sha512sums=('5b53faea10bba16df98a74fcbffebb6c8b7f8c238999a147bb848b2c30732688813915fd6bc32f44ec6f90024ad29dbde059e70760c91ccea198dd05da52d077'
            '3ae61e1c6f0213ea8c3113c62f27c704f04f657cda8c726c0e487194dd5c1ce36e448f6e22b61aeef65f4a1910a2df62a3798f0cba9bae10c8e80a7cb1cfb7b3'
            'SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    local date=$(git log -1 --format="%cd" --date=short | sed s/-//g)
    local count=$(git rev-list --count HEAD)
    local commit=$(git rev-parse --short HEAD)
    echo "$date.r${count}.$commit"
}

build() {
    cd "${pkgname}"
    export GOPATH="${srcdir}/go"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS//-flto=auto/}"
    export CGO_CXXFLAGS="${CXXFLAGS//-flto=auto/}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    go build -trimpath -ldflags "-X github.com/Dreamacro/clash/constant.Version=${pkgver}"
}

check() {
    cd "${pkgname}"
    go test github.com/Dreamacro/clash/... -modcacherw
}

package() {
    cd "${srcdir}/${pkgname}"
    install -Dm755 "clash" "${pkgdir}/usr/bin/clash"
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/clash/LICENSE"
    install -Dm644 "${srcdir}/clash@.service" "${pkgdir}/usr/lib/systemd/system/clash@.service"
    install -Dm644 "${srcdir}/clash_user.service" "${pkgdir}/usr/lib/systemd/user/clash.service"
}
