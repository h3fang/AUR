# Maintainer: h3fang <hf.enigma@gmail.com>
pkgname=tun2socks
pkgver=2.2.1
pkgrel=1
pkgdesc="tun2socks - powered by gVisor TCP/IP stack."
arch=(x86_64)
url="https://github.com/xjasonlyu/tun2socks"
license=(GPL3)
makedepends=(go)
groups=(modified)
source=(
    "https://github.com/xjasonlyu/tun2socks/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('8f226fda878301a8ad625ce1163981fbfdccfdf9a96c4f5c238ec15009932b4ef7c04a53f5f6a332cfc75b8e213f144fbc2fee76c312c4ed14ebc163b1a512d7')

build() {
    cd "$srcdir/${pkgname}-${pkgver}"
    export GOPATH="${srcdir}/go"
    export LDFLAGS=
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    make linux-amd64
}

package() {
    cd "$srcdir/${pkgname}-${pkgver}"
    install -Dm755 "build/tun2socks-linux-amd64" "$pkgdir/usr/bin/tun2socks"
}
