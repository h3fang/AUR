# Maintainer: h3fang <hf.enigma@gmail.com>
pkgname=tun2socks
pkgver=2.4.1
pkgrel=1
pkgdesc="tun2socks - powered by gVisor TCP/IP stack."
arch=(x86_64)
url="https://github.com/xjasonlyu/tun2socks"
license=(GPL3)
makedepends=(go)
groups=(modified)
source=(
    "https://github.com/xjasonlyu/tun2socks/archive/refs/tags/v${pkgver}.tar.gz"
)
sha512sums=('9a95b8cfa3e3997e122849dcbead68d350f80d8d1b0045f54475ac107257f7122b994690048d9de776adcb19f540b5d67f02a0d3178d5ddf38d0072d28ce8d35')

build() {
    cd "$srcdir/${pkgname}-${pkgver}"
    export GOPATH="${srcdir}/go"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    mkdir -p build/
    go build -o build
}

package() {
    cd "$srcdir/${pkgname}-${pkgver}"
    install -Dm755 "build/tun2socks" "$pkgdir/usr/bin/tun2socks"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
