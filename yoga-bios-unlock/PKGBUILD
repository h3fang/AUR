# Maintainer: h3fang <hf.enigma@gmail.com>
pkgname=yoga-bios-unlock-git
_pkgname=yoga-bios-unlock
pkgver=r23.ff8076c
pkgrel=1
pkgdesc="unlock the advanced menu of Lenovo Yoga laptop BIOS"
arch=('x86_64')
url="https://github.com/esno/yoga-bios-unlock"
license=('MIT')
groups=('modified')
depends=('glibc')
source=(
    "git+https://github.com/esno/yoga-bios-unlock"
    "0001-yoga-14sARE-2020.patch"
)
md5sums=('SKIP'
         '74bb5cf267384935e48336bc493db234')

pkgver() {
    cd "$_pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_pkgname"
    patch -Np1 < "$srcdir/0001-yoga-14sARE-2020.patch"
}

build() {
    cd "$srcdir/$_pkgname"
    make
}

package() {
    cd "$srcdir/$_pkgname"
    install -Dm 0644 "$_pkgname" "$pkgdir/usr/bin/$_pkgname"
}

