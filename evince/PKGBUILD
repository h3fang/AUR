# Maintainer: Jos√© Augusto ( a.k.a starlord ) <joseaugusto.881@outlook.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Kazuo Teramoto <kaz.rag@gmail.com>

pkgname=evince
pkgver=44.2
pkgrel=1
pkgdesc="GNOME document viewer, built with minimal dependencies."
url="https://wiki.gnome.org/Apps/Evince"
arch=('i686' 'x86_64')
license=('GPL')
depends=(
    dconf
    djvulibre
    gsfonts
    gspell
    gst-plugins-base-libs
    gtk3
    gvfs
    libarchive
    libgxps
    libhandy
    libsecret
    libspectre
    libsynctex
    poppler-glib
)
makedepends=(
  appstream-glib
  gi-docgen
  git
  gobject-introspection
  meson
  texlive-bin
  yelp-tools
)
optdepends=('texlive-bin: DVI support')
provides=(libev{document,view}3.so)
groups=('gnome' 'modified')
source=("https://github.com/GNOME/evince/archive/refs/tags/${pkgver}.tar.gz")
b2sums=('39d3c0846001dcd9c4a7486075bdcea2d87b1d13022086474085fc42acdd19178a8176e249280b1808f4add4df38dc5797641d43ea99b14878ac4f0880bf33c7')

build() {
    arch-meson evince-${pkgver} build \
    -D ps=enabled \
    -D nautilus=false \
    -D previewer=false \
    -D thumbnailer=false \
    -D gtk_doc=false \
    -D user_doc=false \
    -D keyring=disabled \
    -D thumbnail_cache=disabled
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    meson install -C build --destdir "$pkgdir"
}

