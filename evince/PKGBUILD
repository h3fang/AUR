# Maintainer: Jos√© Augusto ( a.k.a starlord ) <joseaugusto.881@outlook.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Kazuo Teramoto <kaz.rag@gmail.com>

pkgname=evince
pkgver=44.3
pkgrel=1
pkgdesc="GNOME document viewer, built with minimal dependencies."
url="https://wiki.gnome.org/Apps/Evince"
arch=('i686' 'x86_64')
license=('GPL')
depends=(
    dconf
    djvulibre
    gsfonts
    gspell
    gst-plugins-base-libs
    gtk3
    gvfs
    libarchive
    libgxps
    libhandy
    libsecret
    libspectre
    libsynctex
    poppler-glib
)
makedepends=(
  appstream-glib
  gi-docgen
  git
  gobject-introspection
  meson
  texlive-bin
  yelp-tools
)
optdepends=('texlive-bin: DVI support')
provides=(libev{document,view}3.so)
groups=('gnome' 'modified')
source=("https://gitlab.gnome.org/GNOME/evince/-/archive/${pkgver}/evince-${pkgver}.tar.gz")
b2sums=('121fe87d7c01faaccee6985dee0414805e4a28f424119ea0b99cb40f23ec91865ba853c8c125fe205bd305f582c9c75947077d9a1d316394170357444743c230')

build() {
    arch-meson evince-${pkgver} build \
    -D ps=enabled \
    -D nautilus=false \
    -D previewer=false \
    -D thumbnailer=false \
    -D gtk_doc=false \
    -D user_doc=false \
    -D keyring=disabled \
    -D thumbnail_cache=disabled
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    meson install -C build --destdir "$pkgdir"
}

