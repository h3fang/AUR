# Maintainer: Jos√© Augusto ( a.k.a starlord ) <joseaugusto.881@outlook.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Kazuo Teramoto <kaz.rag@gmail.com>

pkgname=evince
pkgver=41.4
pkgrel=1
pkgdesc="GNOME document viewer, built with minimal dependencies by default but configurable."
url="https://wiki.gnome.org/Apps/Evince"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libsm' 'gspell' 'poppler-glib' 'libsynctex' 'libhandy' 'gvfs')
groups=('gnome' 'modified')
makedepends=('itstool' 'intltool' 'python' 'gobject-introspection' 'meson' 'appstream-glib' 'texlive-bin')
optdepends=(
    'texlive-bin: DVI support'
)
provides=(libev{document,view}3.so)
_commit=27a1be1b599ac8e6021622739f79a25f24dfe635 # tags/41.4^0
source=(
  "git+https://gitlab.gnome.org/GNOME/evince.git#commit=$_commit"
)
sha256sums=('SKIP')

pkgver() {
  cd evince
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
    cd evince
    git cherry-pick -n 1060b24d051607f14220f148d2f7723b29897a54
}

build() {
    arch-meson evince build \
    -D nautilus=false \
    -D previewer=false \
    -D thumbnailer=false \
    -D ps=disabled \
    -D djvu=disabled \
    -D dvi=disabled \
    -D xps=disabled \
    -D comics=disabled \
    -D tiff=disabled \
    -D multimedia=disabled \
    -D gtk_doc=false \
    -D user_doc=false \
    -D keyring=disabled \
    -D thumbnail_cache=disabled
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    meson install -C build --destdir "$pkgdir"
}

