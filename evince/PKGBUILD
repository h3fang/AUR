# Maintainer: Jos√© Augusto ( a.k.a starlord ) <joseaugusto.881@outlook.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Kazuo Teramoto <kaz.rag@gmail.com>

pkgname=evince
pkgver=42.2
pkgrel=1
pkgdesc="GNOME document viewer, built with minimal dependencies."
url="https://wiki.gnome.org/Apps/Evince"
arch=('i686' 'x86_64')
license=('GPL')
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre dconf libsecret libsynctex
         libarchive gst-plugins-base-libs gspell libhandy gvfs)
makedepends=(texlive-bin gobject-introspection gi-docgen git meson appstream-glib yelp-tools)
optdepends=('texlive-bin: DVI support')
provides=(libev{document,view}3.so)
groups=('gnome' 'modified')
source=("https://github.com/GNOME/evince/archive/refs/tags/${pkgver}.tar.gz")
b2sums=('65b5c851859f2ef0be6c5035a609176f058d5efce4a6e4cfe8fbc8ff2341fcc8e5cc66bf2bee6ae5978ddaa432be27097bbc88c984846ed6339dbc5cd2023f90')

build() {
    arch-meson evince-${pkgver} build \
    -D ps=enabled \
    -D nautilus=false \
    -D previewer=false \
    -D thumbnailer=false \
    -D gtk_doc=false \
    -D user_doc=false \
    -D keyring=disabled \
    -D thumbnail_cache=disabled
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    meson install -C build --destdir "$pkgdir"
}

