# Maintainer: Jos√© Augusto ( a.k.a starlord ) <joseaugusto.881@outlook.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Kazuo Teramoto <kaz.rag@gmail.com>

pkgname=evince
pkgver=40.1
pkgrel=1
pkgdesc="GNOME document viewer, built with minimal dependencies by default but configurable."
url="https://wiki.gnome.org/Apps/Evince"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libsm' 'gspell' 'poppler-glib' 'libsynctex' 'libhandy')
groups=('gnome' 'modified')
makedepends=('itstool' 'intltool' 'python' 'gobject-introspection' 'meson' 'appstream-glib' 'texlive-bin')
optdepends=(
    'texlive-bin: DVI support'
    'gvfs: bookmark support and session saving'
)
provides=(libev{document,view}3.so)
_commit=133f1c1880fd97d432442c8f7956c2c4a1efd5dc  # tags/40.1^0
source=("git+https://gitlab.gnome.org/GNOME/evince.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
    arch-meson $pkgname build \
    -D nautilus=false \
    -D browser_plugin=false \
    -D previewer=false \
    -D thumbnailer=false \
    -D ps=disabled \
    -D djvu=disabled \
    -D dvi=disabled \
    -D xps=disabled \
    -D comics=disabled \
    -D tiff=disabled \
    -D multimedia=disabled \
    -D gtk_doc=false \
    -D user_doc=false \
    -D keyring=disabled \
    -D thumbnail_cache=disabled
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}

