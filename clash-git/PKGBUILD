# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ariel AxionL <i@axionl.me>
# Contributor: Luke Yue <lukedyue@gmail.com>

pkgname=clash-git
_pkgver=0.19.0
pkgver=0.19.0.r11.g84f627f
pkgrel=1
pkgdesc="A rule based proxy in Go"
arch=('x86_64')
url="https://github.com/Dreamacro/clash"
license=('GPL3')
makedepends=('go-pie' 'git')
depends=('glibc')
provides=('clash')
source=("clash@.service"
        "clash_user.service"
        "$pkgname::git+https://github.com/Dreamacro/clash.git#branch=dev")
sha512sums=('e9e215e0a06d0d1072467342dd72e777725b94634714c5bd5fdee31f48897d9b8ab7e83830d836533d6862fa6ebfda56dbf138edd873140de61d8ccdf2a54572'
            '338c3a5904623bec48c03b2ef8cf452979c229fc5b89b2e0447664b40cd6e29c83cae47a19145be76d3ee2f0b6a54184a0cff69b834ba6107b444caacc02decf'
            'SKIP')

pkgver() {
    cd "$pkgname"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${pkgname}"
    go build -trimpath -ldflags "-extldflags ${LDFLAGS} -X github.com/Dreamacro/clash/constant.Version=${_pkgver} -w -s" -mod=readonly
}

check() {
    cd "${pkgname}"
    go test github.com/Dreamacro/clash/...
}

package() {
    cd "${srcdir}/${pkgname}"
    install -Dm755 "clash" "${pkgdir}/usr/bin/clash"
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/clash/LICENSE"
    install -Dm644 "${srcdir}/clash@.service" "${pkgdir}/usr/lib/systemd/system/clash@.service"
    install -Dm644 "${srcdir}/clash_user.service" "${pkgdir}/usr/lib/systemd/user/clash.service"
}
