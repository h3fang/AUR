# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

pkgname=openblas
_pkgname=OpenBLAS
pkgver=0.3.8
pkgrel=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD"
arch=('x86_64')
url="http://www.openblas.net/"
license=('BSD')
groups=('modified')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran')
provides=('blas=3.9.0' 'cblas=3.9.0' 'lapack=3.9.0')
conflicts=('blas' 'cblas' 'lapack')
source=(${_pkgname}-v${pkgver}.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha512sums=('d557a332b1f905399d97dd5392ca10ca4eed47d669cae4abea374ed7c2e6c1ab29a4415df1224e940b9041e1545fa5ede2bdfb266986230436014795e7d3289d')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  make NO_STATIC=1 NO_LAPACKE=1 NO_AFFINITY=1 USE_OPENMP=1 CFLAGS="$CPPFLAGS $CFLAGS" \
       MAJOR_VERSION=3 libs netlib shared
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make PREFIX="$pkgdir"/usr MAJOR_VERSION=3 install
  rm -f "$pkgdir"/usr/include/lapacke*
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$pkgdir"/usr/lib/
  # BLAS
  ln -sf libopenblas_*.so libblas.so
  ln -sf libopenblas_*.so libblas.so.3

  # CBLAS
  ln -sf libopenblas_*.so libcblas.so
  ln -sf libopenblas_*.so libcblas.so.3
  ln -sf libopenblas_*.so libcblas.so.3.9.0

  # LAPACK
  ln -sf libopenblas_*.so liblapack.so
  ln -sf libopenblas_*.so liblapack.so.3
  ln -sf libopenblas_*.so liblapack.so.3.9.0

  sed -i -e "s%$pkgdir%%" "$pkgdir"/usr/lib/cmake/openblas/OpenBLASConfig.cmake
  sed -i -e "s%$pkgdir%%" "$pkgdir"/usr/lib/pkgconfig/openblas.pc
  ln -s openblas.pc "$pkgdir"/usr/lib/pkgconfig/blas.pc

  rmdir "$pkgdir"/usr/bin
}

# vim:set ts=2 sw=2 et:
