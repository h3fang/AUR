# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

pkgname=openblas
_pkgname=OpenBLAS
pkgver=0.3.21
pkgrel=4
_blasver=3.9.0
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD"
arch=('x86_64')
url="http://www.openblas.net/"
license=('BSD')
groups=('modified')
depends=('gcc-libs')
makedepends=('cmake' 'perl' 'gcc-fortran')
provides=("blas=$_blasver" "cblas=$_blasver" "lapack=$_blasver")
conflicts=('blas' 'cblas' 'lapack')
source=(
  ${_pkgname}-v${pkgver}.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz
  https://github.com/xianyi/OpenBLAS/commit/7e97dddb.patch
)
sha512sums=('4625c8e6ccfa9120281fd714d3f6b7c3ba2265470c1be76121d6b25dc3dacb899d26e5d9a417ddc616d23909f1411495aa995ef8d8d6df8511cd5cefbabcb1c5'
            'fd5b31176a073e9fad95b99d801be2350c6b957595b89f649f84cc7f938177a4d725f7c29f5e785c5441bcd89b5a2b6ddb9e77357dce1fe58fa9814cc8c1ea34')

prepare() {
  # Fix libm linkage https://github.com/xianyi/OpenBLAS/issues/3892
  patch -d $_pkgname-$pkgver -p1 < 7e97dddb.patch
}

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTING=OFF \
    -DNO_AFFINITY=ON \
    -DUSE_OPENMP=1 \
    -DNO_WARMUP=1 \
    -DNUM_THREADS=64
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $_pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$pkgdir"/usr/lib/
  # BLAS
  ln -sf libopenblas_*.so libblas.so
  ln -sf libopenblas_*.so libblas.so.3

  # CBLAS
  ln -sf libopenblas_*.so libcblas.so
  ln -sf libopenblas_*.so libcblas.so.3
  ln -sf libopenblas_*.so libcblas.so.$_blasver

  # LAPACK
  ln -sf libopenblas_*.so liblapack.so
  ln -sf libopenblas_*.so liblapack.so.3
  ln -sf libopenblas_*.so liblapack.so.$_blasver

  ln -s openblas.pc "$pkgdir"/usr/lib/pkgconfig/blas.pc
}

# vim:set ts=2 sw=2 et:
