_gitname=swaylock-effects
pkgname="${_gitname}-git"
pkgver=r405.b2736c5
pkgrel=1
pkgdesc="A fancier screen locker for Wayland."
arch=('i686' 'x86_64')
url="https://github.com/jirutka/$_gitname"
license=('MIT')
depends=('libxkbcommon' 'cairo' 'gdk-pixbuf2' 'pam')
makedepends=('git' 'meson' 'ninja' 'scdoc' 'wayland' 'wayland-protocols')
provides=('swaylock' 'swaylock-effects')
conflicts=('swaylock')
groups=('modified')
source=("git+https://github.com/jirutka/${_gitname}.git"
        '01-text-align.patch')
b2sums=('SKIP'
        'c56bcc536ec2d366e156285ae6c3c8b5cc263fd7a2b9c672ba112814b9993e4cc48533f2cc0c1d490ed9ac9d6627f33fa9db34dbd296d21f5f56ea98404dc0bf')

pkgver() {
    cd "$srcdir/$_gitname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"
    patch -p1 < "$srcdir"/01-text-align.patch
}

build() {
    cd "$srcdir/$_gitname"
    meson build --prefix=/usr
    ninja -C build
}

package() {
    cd "$srcdir/$_gitname"
    DESTDIR="$pkgdir" ninja -C build install
}
