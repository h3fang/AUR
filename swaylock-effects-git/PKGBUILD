# Maintainer: Martin DÃ¸rum <martid0311@gmail.com>
_gitname=swaylock-effects
pkgname="${_gitname}-git"
pkgver=r354.912c8bc
pkgrel=1
pkgdesc="A fancier screen locker for Wayland."
arch=('i686' 'x86_64')
url="https://github.com/mortie/$_gitname"
license=('MIT')
depends=('libxkbcommon' 'cairo' 'gdk-pixbuf2' 'pam')
makedepends=('git' 'meson' 'ninja' 'scdoc')
provides=('swaylock' 'swaylock-effects')
conflicts=('swaylock')
groups=('modified')
source=("git+https://github.com/mortie/${_gitname}.git"
        '01-text-align.patch')
md5sums=('SKIP' 'SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"
    patch -p1 < "$srcdir"/01-text-align.patch
}

build() {
    cd "$srcdir/$_gitname"
    meson build --prefix=/usr
    ninja -C build
}

package() {
    cd "$srcdir/$_gitname"
    DESTDIR="$pkgdir" ninja -C build install
}
