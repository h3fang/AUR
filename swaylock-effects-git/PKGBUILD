_gitname=swaylock-effects
pkgname="${_gitname}-git"
pkgver=r405.b2736c5
pkgrel=2
pkgdesc="A fancier screen locker for Wayland."
arch=('i686' 'x86_64')
url="https://github.com/jirutka/$_gitname"
license=('MIT')
depends=('libxkbcommon' 'cairo' 'gdk-pixbuf2' 'pam')
makedepends=('git' 'meson' 'ninja' 'scdoc' 'wayland' 'wayland-protocols')
provides=('swaylock' 'swaylock-effects')
conflicts=('swaylock')
groups=('modified')
source=("git+https://github.com/jirutka/${_gitname}.git"
        '01-fix-time-string-postion-and-scale.patch')
b2sums=('SKIP'
        '7edfa90b6d7be116980f86cb0849a3244d259abc5659f8f92844767b2e454d07c9cc3b7a6b07c78de3511cf62628f210bfddbfdc37706e5ec695a4e3b552e68e')

pkgver() {
    cd "$srcdir/$_gitname"
    echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$_gitname"
    patch -p1 < "$srcdir"/01-fix-time-string-postion-and-scale.patch
}

build() {
    cd "$srcdir/$_gitname"
    meson build --prefix=/usr
    ninja -C build
}

package() {
    cd "$srcdir/$_gitname"
    DESTDIR="$pkgdir" ninja -C build install
}
