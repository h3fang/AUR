# Maintainer: David McInnis <dave@dave3.xyz>
# Contributer: Fabien Dubosson <fabien.dubosson@gmail.com>

pkgname=python-keras
_pkgname=keras
pkgver=2.2.4
pkgrel=1
pkgdesc="Deep Learning library (convnets, recurrent neural networks, and more)"
arch=('any')
url="https://github.com/fchollet/keras"
license=('MIT')
depends=('python' 'python-numpy' 'python-scipy' 'python-keras-applications' 'python-keras-preprocessing' 'python-h5py' 'python-yaml')
optdepends=('python-theano' 'python-tensorflow' 'cudnn')
makedepends=('python-setuptools' 'python-numpy'  'python-scipy'  'python-h5py'  'python-yaml')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/keras-team/${_pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('46f8e5bd66f778abd8d5a62b3c3d749fbd41854176fcf0df5258cf94c3fd1b28')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

