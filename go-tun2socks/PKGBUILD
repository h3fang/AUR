# Maintainer: DDoSolitary <DDoSolitary@gmail.com>
pkgname=go-tun2socks
pkgver=1.16.11
pkgrel=2
pkgdesc="A tun2socks implementation written in Go."
arch=(x86_64)
url=https://github.com/eycorsican/go-tun2socks
license=(MIT)
makedepends=(go git)
groups=(modified)
source=(
    $pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
)
sha512sums=('e64bab61feefadfa5af1752b7b7dbad78d4d1ba706e7b07a130c1a7a78638036cc439f394fbdc92337eb718acc50f2b6b58b46eb2a248e698eaafbd75c0ceeb4')

build() {
    export GOPATH="${srcdir}/go"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS//-flto=auto/}"
    export CGO_CXXFLAGS="${CXXFLAGS//-flto=auto/}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    cd "$srcdir/$pkgname-$pkgver"
    make VERSION=v$pkgver
}

package() {
    install -Dm755 "$srcdir/$pkgname-$pkgver/build/tun2socks" "$pkgdir/usr/bin/go-tun2socks"
}

