# Maintainer: Jos√© Augusto ( a.k.a starlord ) <joseaugusto.881@outlook.com>
# Contributor: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>
# Contributor: Kazuo Teramoto <kaz.rag@gmail.com>

_pkgname=evince
pkgname=${_pkgname}-light
pkgver=3.37.90
pkgrel=1
pkgdesc="GNOME document viewer, built with minimal dependencies by default but configurable."
url="https://wiki.gnome.org/Apps/Evince"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libsm' 'gspell')

makedepends=('itstool' 'intltool' 'python' 'gobject-introspection' 'meson' 'appstream-glib')

[[ "${_nautilus}"   == "yes" ]] && makedepends+=('libnautilus-extension' 'gnome-common')

optdepends=('gvfs: bookmark support and session saving')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("https://ftp.gnome.org/pub/GNOME/sources/${_pkgname}/${pkgver:0:4}/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('2a878043b9210ad7ab70805f83e400654e214e1f5ef960100adf938832d02eaf')

build() {
    arch-meson ${_pkgname}-${pkgver} build \
    -D nautilus=false \
    -D browser_plugin=false \
    -D previewer=false \
    -D thumbnailer=false \
    -D ps=disabled \
    -D djvu=disabled \
    -D dvi=disabled \
    -D xps=disabled \
    -D comics=disabled \
    -D tiff=disabled \
    -D multimedia=disabled \
    -D gtk_doc=false \
    -D user_doc=false \
    -D keyring=disabled \
    -D thumbnail_cache=disabled
    meson compile -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    DESTDIR="$pkgdir" meson install -C build
}
