# Maintainer: Florian Jacob <projects+arch AT florianjacob )DOT( de>
# Maintainer: Jake <aur@ja-ke.tech>
pkgname=earlyoom
pkgver=1.2
pkgrel=2
pkgdesc="Early OOM Daemon for Linux"
arch=('any')
url="https://github.com/rfjakob/earlyoom"
license=('MIT')
backup=("etc/default/earlyoom")
source=(
	"https://github.com/rfjakob/$pkgname/archive/v$pkgver.tar.gz"
)
sha512sums=('45edc7064f227fce3ed6fdc9e524a8a117f8a9de50d98c9e75f31c193a1820804df8d9fac02c4ee1daabd3c2018bbeb422611ca29971e1c11f86446de8c96bc1')

build() {
	cd "$pkgname-$pkgver"

	# earlyoom tries to get the version number from the git repo, but we're packaging tarballs here, so that would fail.
	make VERSION=$pkgver earlyoom
}

package() {
	cd "$pkgname-$pkgver"	
	make install DESTDIR="${pkgdir}" PREFIX="/usr" SYSTEMDDIR="/usr/lib/systemd"
}
