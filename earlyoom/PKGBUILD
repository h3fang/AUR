# Maintainer: Florian Jacob <projects+arch AT florianjacob )DOT( de>
# Maintainer: Jake <aur@ja-ke.tech>
pkgname=earlyoom
pkgver=1.5
pkgrel=1
pkgdesc="Early OOM Daemon for Linux"
arch=('any')
url="https://github.com/rfjakob/earlyoom"
license=('MIT')
groups=('modified')
backup=("etc/default/earlyoom")
source=(
	"https://github.com/rfjakob/$pkgname/archive/v$pkgver.tar.gz"
)
sha512sums=('e6c813e184e3f88398dd0a079f992e7cc0532f0b38b587321d0bd74c68df77c182bfcbd11abd19ff4928e5a9d10ae82f16561a6b8bf4990e78b291ae1aa6f7ab')

prepare() {
    cd "$pkgname-$pkgver"
    # remove useless command for Arch package
    sed -i "/systemctl/d;/chcon/d" Makefile
}

build() {
	cd "$pkgname-$pkgver"

	# earlyoom tries to get the version number from the git repo, but we're packaging tarballs here, so that would fail.
	make VERSION=$pkgver earlyoom
}

package() {
	cd "$pkgname-$pkgver"
	make install-bin install-default earlyoom.service DESTDIR="${pkgdir}" PREFIX="/usr"
	install -Dm 644 earlyoom.service "${pkgdir}/usr/lib/systemd/system/earlyoom.service"
}
