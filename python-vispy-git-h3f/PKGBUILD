# Maintainer: Antony Lee <anntzer dot lee at gmail dot com>

pkgname=python-vispy-git
pkgver=v0.6.1.r14.g8b61cd43
pkgrel=1
pkgdesc='A high-performance interactive 2D/3D data visualization library.'
arch=('any')
url='https://github.com/vispy/vispy/'
license=('BSD')
groups=('modified')
depends=('python-numpy')
makedepends=('cython')
optdepends=('python-glfw: a possible backend'
            'python-pyglet: a possible backend'
            'python-pysdl2: a possible backend'
            'python-pyqt5: a possible backend'
            'pyside2: a possible backend')
source=("$pkgname::git+https://github.com/vispy/vispy.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  cd "$srcdir/$pkgname"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

