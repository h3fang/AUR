# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libblockdev
pkgver=2.26
pkgrel=1
pkgdesc="A library for manipulating block devices"
arch=('x86_64')
url="https://github.com/rhinstaller/libblockdev"
license=('LGPL')
groups=('modified')
depends=('dosfstools' 'dmraid' 'gptfdisk' 'libbytesize' 'mdadm' 'ndctl'
         'parted' 'volume_key' 'kmod' 'libyaml')
makedepends=('autoconf-archive' 'gobject-introspection' 'systemd' 'python')
source=("https://github.com/storaged-project/libblockdev/releases/download/${pkgver}-1/libblockdev-${pkgver}.tar.gz")
sha512sums=('ddd4e9c22135bd7dad0bb9bc254ac0c63e3bacc592e7c5a0c846e367da3fc248bf6187f1c81407c3bce599d3b0ceeec9a8a0030ad8e981e245f715b35eaec523')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --without-lvm_dbus --without-lvm --without-btrfs
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
