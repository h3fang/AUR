# Maintainer: Fang He <hf.enigma@gmail.com>

pkgname=texlive-fontsextra
pkgver=2021.62623
_revnr=${pkgver#2021.}
pkgrel=1
pkgdesc="TeX Live - extra fonts that I need"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-most' 'modified')
url='http://tug.org/texlive/'
source=(
    "https://ctan.org/tex-archive/systems/texlive/tlnet/archive/fira.tar.xz"
    "https://ctan.org/tex-archive/systems/texlive/tlnet/archive/firamath.tar.xz"
    "https://ctan.org/tex-archive/systems/texlive/tlnet/archive/firamath-otf.tar.xz"
    "https://ctan.org/tex-archive/systems/texlive/tlnet/archive/fontawesome.tar.xz"
    "https://ctan.org/tex-archive/systems/texlive/tlnet/archive/fontawesome5.tar.xz"
    "https://ctan.org/tex-archive/systems/texlive/tlnet/archive/sourcecodepro.tar.xz"
    "https://ctan.org/tex-archive/systems/texlive/tlnet/archive/sourcesanspro.tar.xz"
    "https://ctan.org/tex-archive/systems/texlive/tlnet/archive/newtx.tar.xz"
    "https://ctan.org/tex-archive/systems/texlive/tlnet/archive/xits.tar.xz"
    "https://raw.githubusercontent.com/archlinux/svntogit-packages/packages/texlive-fontsextra/trunk/texlive-fontsextra.maps"
)
options=('!emptydirs' '!strip')
sha256sums=('fd3e420a2204d1296e882b6cb1c4d218fc28f1abdac370710587caf406835df4'
            '778baeecbcc6bd2c55611dcfa6985212d6e759110861fbd19be7f7843542fff3'
            'c55add2adb27e1258a6cc335f0149d5d53662b68f243d57f97bafca83042141a'
            'a2e24ac5e56f16af8b6c4b7e95b190f44d7b631e1072c6abc0a2ff581750e242'
            '22c2a9a62d330c0e19507a70781a4ccecb779a0d4da3b3ff963e618305f1bc78'
            '1ec9052d3e9664439eec2449dbe3671e076eab45ceeae2deebfff16dde854c26'
            '1c9558be99163e277388d76c8017495e267e87f51b7749b2bb43827b77aadbde'
            '19666150404bd8d47a12acf782cc557b5e8fcb5d4943978ffa93e343c6bbe2ad'
            'cdc51390b6ad6e1151d0186c5be4b24d8bc910e338fa472db282023825c74409'
            '65e004cc5530a3f31d4213f56cae6b9318644ec14246e606819dbfda6109ab7f')

build() {
   cd "$srcdir"
   touch CONTENTS
   touch MAPS
   for p in tlpkg/tlpobj/*.tlpobj; do
       pstem=${p#tlpkg/tlpobj/}
       pstem=${pstem%.tlpobj}
       echo $pstem $(grep revision $p | awk '{print $2}') >> CONTENTS
       echo $(grep -i $pstem.map $pkgname.maps) >> MAPS
   done
   sed -i '/^$/d' MAPS
   rm -rf {tlpkg,doc,source} || true
}

package() {
   cd "$srcdir"
   install -m755 -d "$pkgdir"/var/lib/texmf/arch/installedpkgs
   install -m644 CONTENTS "$pkgdir"/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 MAPS "$pkgdir"/var/lib/texmf/arch/installedpkgs/$pkgname.maps
   install -m755 -d "$pkgdir"/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 "$pkgdir"/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' "$pkgdir"/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 "$pkgdir"/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' "$pkgdir"/usr/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/usr/share/texmf-dist/scripts ]]; then
     find "$pkgdir"/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
}
