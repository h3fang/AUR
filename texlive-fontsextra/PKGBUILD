# Maintainer: Fang He <hf.enigma@gmail.com>

pkgname=texlive-fontsextra
pkgver=2023.66328
_revnr=${pkgver#2023.}
pkgrel=1
pkgdesc="TeX Live - extra fonts that I need"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-most' 'modified')
url='http://tug.org/texlive/'
source=(
    "https://mirrors.sjtug.sjtu.edu.cn/ctan/systems/texlive/tlnet/archive/fira.tar.xz"
    "https://mirrors.sjtug.sjtu.edu.cn/ctan/systems/texlive/tlnet/archive/firamath.tar.xz"
    "https://mirrors.sjtug.sjtu.edu.cn/ctan/systems/texlive/tlnet/archive/firamath-otf.tar.xz"
    "https://mirrors.sjtug.sjtu.edu.cn/ctan/systems/texlive/tlnet/archive/fontawesome.tar.xz"
    "https://mirrors.sjtug.sjtu.edu.cn/ctan/systems/texlive/tlnet/archive/fontawesome5.tar.xz"
    "https://mirrors.sjtug.sjtu.edu.cn/ctan/systems/texlive/tlnet/archive/sourcecodepro.tar.xz"
    "https://mirrors.sjtug.sjtu.edu.cn/ctan/systems/texlive/tlnet/archive/sourcesanspro.tar.xz"
    "https://mirrors.sjtug.sjtu.edu.cn/ctan/systems/texlive/tlnet/archive/newtx.tar.xz"
    "https://mirrors.sjtug.sjtu.edu.cn/ctan/systems/texlive/tlnet/archive/xits.tar.xz"
    "https://raw.githubusercontent.com/archlinux/svntogit-packages/packages/texlive-fontsextra/trunk/texlive-fontsextra.maps"
)
options=('!emptydirs' '!strip')
sha256sums=('ddd01a7538ab53e272c7282eb8f072a3baa76b8ad30034ff3e5d87cb77a7e252'
            '778baeecbcc6bd2c55611dcfa6985212d6e759110861fbd19be7f7843542fff3'
            'f6c4d7eff5a3c235c3c88b504ed1bda04193d48991d6cfd4e5102a8c4cad81ed'
            'a2e24ac5e56f16af8b6c4b7e95b190f44d7b631e1072c6abc0a2ff581750e242'
            '4d85aab86f679d360e7c7c812a055d9c03dd658e31bccfca4a97604497b0cc1c'
            '1ec9052d3e9664439eec2449dbe3671e076eab45ceeae2deebfff16dde854c26'
            '1c9558be99163e277388d76c8017495e267e87f51b7749b2bb43827b77aadbde'
            '50bfa3e5182ce32281cef269dbb8d4d2b727899391df1ef6c1ca1be2108e215e'
            'cdc51390b6ad6e1151d0186c5be4b24d8bc910e338fa472db282023825c74409'
            '65e004cc5530a3f31d4213f56cae6b9318644ec14246e606819dbfda6109ab7f')

build() {
   cd "$srcdir"
   touch CONTENTS
   touch MAPS
   for p in tlpkg/tlpobj/*.tlpobj; do
       pstem=${p#tlpkg/tlpobj/}
       pstem=${pstem%.tlpobj}
       echo $pstem $(grep revision $p | awk '{print $2}') >> CONTENTS
       echo $(grep -i $pstem.map $pkgname.maps) >> MAPS
   done
   sed -i '/^$/d' MAPS
   rm -rf {tlpkg,doc,source} || true
}

package() {
   cd "$srcdir"
   install -m755 -d "$pkgdir"/var/lib/texmf/arch/installedpkgs
   install -m644 CONTENTS "$pkgdir"/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 MAPS "$pkgdir"/var/lib/texmf/arch/installedpkgs/$pkgname.maps
   install -m755 -d "$pkgdir"/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 "$pkgdir"/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' "$pkgdir"/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 "$pkgdir"/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' "$pkgdir"/usr/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/usr/share/texmf-dist/scripts ]]; then
     find "$pkgdir"/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
}
