# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 

pkgbase=python-numpy
pkgname=('python-numpy')
pkgver=1.15.0
pkgrel=2
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="http://www.numpy.org/"
makedepends=('cblas' 'lapack' 'python' 'python-setuptools' 'gcc-fortran'
             'python-nose' 'cython')
options=('staticlibs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/numpy/numpy/archive/v$pkgver.tar.gz")
sha512sums=('1ee1c6cdf4ae287f1a0e7c49533c143c5842817cd92681cf72507f32904866de2e1fdca648f09fdca9fae391c78cebdd9a32a7116ff10ca3887f6f5ab18763b2')

build() {
  cd "$srcdir"/numpy-$pkgver
  CFLAGS="-march=native"
  CXXFLAGS="-march=native"
  python setup.py build -j 8
}

package() {
  depends=('cblas' 'lapack' 'python')
  optdepends=('python-nose: testsuite'
              'openblas: faster linear algebra')

  cd numpy-$pkgver
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}/usr/share/licenses/python-numpy"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/python-numpy/"
}

