# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 

pkgbase=python-numpy
pkgname=('python-numpy')
pkgver=1.14.0
pkgrel=1
pkgdesc="Scientific tools for Python"
arch=('x86_64')
license=('custom')
url="http://www.numpy.org/"
depends=('lapack' 'python' 'cblas')
optdepends=('python-nose: testsuite')
provides=("python3-numpy=${pkgver}")
replaces=('python3-numpy')
conflicts=('python3-numpy')
makedepends=('cblas' 'lapack' 'python' 'python-setuptools' 'gcc-fortran' 'python-nose' 'cython')
options=('staticlibs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/numpy/numpy/archive/v$pkgver.tar.gz")
sha512sums=('4ebb8d34fefc9043d325b5381c71c0ec437649a9116a4518f0af2d5c9a4d3254073c694aaf1ac02368b263673924fe5234341c7d88dd6032a93ea70cf6c0b8bf')

build() {
  export CFLAGS="-fopenmp -m64 -mtune=native -Wl,--no-as-needed"
  export CXXFLAGS="-fopenmp -m64 -mtune=native -Wl,--no-as-needed"
  export LDFLAGS="-ldl -lm"
  export FFLAGS="-fopenmp -m64 -mtune=native"
  export LDFLAGS="$LDFLAGS -shared"

  echo "Building Python3"
  cd numpy-$pkgver
  python setup.py config_fc --fcompiler=gnu95 build -j 8
}

package_python-numpy() {
  _pyver=3.6
  _pyinc=3.6m

  export ATLAS=None
  export LDFLAGS="$LDFLAGS -shared"

  cd numpy-$pkgver
  python setup.py config_fc --fcompiler=gnu95 install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -m755 -d "${pkgdir}/usr/share/licenses/python-numpy"
  install -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/python-numpy/"

  install -m755 -d "${pkgdir}/usr/include/python${_pyinc}"
  ln -sf /usr/lib/python${_pyver}/site-packages/numpy/core/include/numpy "${pkgdir}/usr/include/python${_pyinc}/numpy"
}
