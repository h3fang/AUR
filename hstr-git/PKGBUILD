# Maintainer: Christian Wieden <wiedenchr at gmail dot com
# Contributer: Ricardo Band <me at xengi dot de>
# Contributor: Filip Szyma≈Ñski <fszymanski at, fedoraproject.org>

pkgname=hstr-git
pkgver=2.5.r13.gbe59f8c
pkgrel=1
pkgdesc="A command line utility that brings improved BASH command completion from the history. It aims to make completion easier and more efficient than Ctrl-r."
arch=('any')
url="https://github.com/dvorka/hstr"
license=('Apache')
makedepends=('git' 'readline' 'ncurses')
depends=('readline')
groups=('modified')
source=(
    "hstr::git+https://github.com/dvorka/hstr.git"
    "01-remove-history-r.patch"
)
sha256sums=('SKIP'
            '7554658805a8f32d642fbdc03fcc0e5c23d3964e2992ea2c6abc1895b6728539')
conflicts=('hh');

prepare() {
    cd "$srcdir/hstr"
    for f in "$srcdir"/*.patch; do
        echo "Applying patch $f ..."
        git apply "$f"
    done
}

build() {
    cd "hstr/build/tarball"
    ./tarball-automake.sh
    cd ../..
    sed -i -e "s#<ncursesw/curses.h>#<curses.h>#g" src/include/hstr_curses.h
    sed -i -e "s#<ncursesw/curses.h>#<curses.h>#g" src/include/hstr.h
    ./configure --prefix=/usr
    make
}

package() {
    cd "hstr"
    make DESTDIR="$pkgdir/" install
}

pkgver() {
    cd "hstr"
    git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}
