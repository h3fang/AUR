# Maintainer: Christian Wieden <wiedenchr at gmail dot com
# Contributer: Ricardo Band <me at xengi dot de>
# Contributor: Filip Szyma≈Ñski <fszymanski at, fedoraproject.org>

pkgname=hstr-git
pkgver=2.3.r0.g60fa279
pkgrel=1
pkgdesc="A command line utility that brings improved BASH command completion from the history. It aims to make completion easier and more efficient than Ctrl-r."
arch=('any')
url="https://github.com/dvorka/hstr"
license=('Apache')
makedepends=('git' 'readline' 'ncurses')
depends=('readline')
groups=('modified')
source=(git://github.com/dvorka/hstr.git#branch=master
    01-remove-history-r.patch)
sha256sums=('SKIP'
            'e7cd48e0d87301d02615ea59ef353382c391aac79beb6b4fd1f4871726c363c1')
conflicts=('hh');

prepare() {
    cd "$srcdir/hstr"
    echo "Applying patches..."
    git apply "$srcdir/01-remove-history-r.patch"
}

build() {
    cd "hstr/build/tarball"
    ./tarball-automake.sh
    cd ../..
    sed -i -e "s#<ncursesw/curses.h>#<curses.h>#g" src/include/hstr_curses.h
    sed -i -e "s#<ncursesw/curses.h>#<curses.h>#g" src/include/hstr.h
    ./configure --prefix=/usr
    make
}

package() {
    cd "hstr"
    make DESTDIR="$pkgdir/" install
}

pkgver() {
    cd "hstr"
    git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}
