# Maintainer: peeweep <peeweep at 0x0 dot ee>

pkgname=clash-dashboard-git
_pkgname=clash-dashboard
pkgver=20200626.e531d5a
pkgrel=1
pkgdesc="Web UI for clash"
arch=('any')
url="https://github.com/Dreamacro/clash-dashboard"
license=('MIT')
makedepends=('npm' 'nodejs' 'git')
conflicts=('clash-dashboard')
provides=('clash-dashborad')
source=("${_pkgname}::git+https://github.com/Dreamacro/clash-dashboard.git")
sha1sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  npm install --cache "${srcdir}/npm-cache"
}

build() {
  cd "${srcdir}/${_pkgname}"
  npm run-script build
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -dm755 "${pkgdir}/usr/share"
  cp -rT dist "${pkgdir}/usr/share/${_pkgname}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
