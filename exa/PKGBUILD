# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lucas Raab <tuftedocelot@fastmail.fm>

pkgname=exa
pkgver=0.9.0
pkgrel=2
pkgdesc='ls replacement'
arch=(x86_64)
url='https://the.exa.website/'
license=(MIT)
depends=(libssh2 zlib)
makedepends=(cargo cmake git rust)
groups=('modified')
source=("git+https://github.com/ogham/exa.git")
md5sums=('SKIP')

build() {
  cd $pkgname
  cargo build --release
}

check() {
  cd $pkgname
  cargo test
}

package() {
  cd $pkgname

  mkdir -p "$pkgdir/usr/bin"
  cp target/release/exa "$pkgdir/usr/bin"

  install -Dm644 completions/completions.bash \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 completions/completions.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm644 completions/completions.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 LICEN?E \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
