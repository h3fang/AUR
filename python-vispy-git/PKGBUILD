# Maintainer: Antony Lee <anntzer dot lee at gmail dot com>

pkgname=python-vispy-git
pkgver=v0.5.0.r361.g521485d8
pkgrel=1
pkgdesc='A high-performance interactive 2D/3D data visualization library.'
arch=('any')
url='https://github.com/vispy/vispy/'
license=('BSD')
depends=('python-numpy')
makedepends=('cython')
optdepends=('python-glfw: a possible backend'
            'python-pyglet: a possible backend'
            'python-pysdl2: a possible backend'
            'python-pyqt5: a possible backend'
            'pyside2: a possible backend')
source=("$pkgname::git+https://github.com/vispy/vispy.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd "$pkgname"
    git submodule update --init --recursive
}

# build() {
#   cd "$srcdir/$pkgname"
#   python setup.py build
# }

package() {
  cd "$srcdir/$pkgname"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

